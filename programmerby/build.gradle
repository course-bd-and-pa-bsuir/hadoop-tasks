apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'

sourceCompatibility = 1.6
targetCompatibility = 1.6
version = '1.0'

repositories {
    mavenCentral()
    maven {
        url 'https://repository.cloudera.com/artifactory/cloudera-repos/'
    }
}

configurations {
    provided
}

sourceSets {
    main {
        compileClasspath += configurations.provided
    }
}

idea {
    module {
        // (заменил PROVIDED -> COMPILE) Чтобы можно было запустить прямо из IDEA
        scopes.COMPILE.plus += [ configurations.provided ]
    }
}

eclipse {
    classpath {
        defaultOutputDir = file("${buildDir}/eclipse-classes")
        plusConfigurations += [ configurations.provided ]
        noExportConfigurations += [ configurations.provided ]
        downloadSources = true
    }
}

dependencies {
    provided 'org.apache.hadoop:hadoop-client:2.5.0-cdh5.3.3'

    compile 'com.codesnippets4all:quick-json:1.0.4'

    testCompile group: 'junit', name: 'junit', version: '4.11'
}

// Hadoop использует версию guava 11.0.2, но какая-то зависимость ставит версию 16.0.
// Принудительно сделаем 11.0.2
configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        if (details.requested.group == 'com.google.guava') {
            details.useVersion '11.0.2'
        }
    }
}

// Emulate Maven shade plugin with a fat jar.
// http://docs.codehaus.org/display/GRADLE/Cookbook#Cookbook-Creatingafatjar
jar {
    from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
}
